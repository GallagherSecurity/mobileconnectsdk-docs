apply plugin: 'com.android.application'

repositories {
    maven { url "file://$projectDir/gallagher-mobile-access" }
    maven { url "file://$projectDir/salto-justinmobile-sdk" }
}

android {
    compileSdkVersion 34
    buildToolsVersion = '34.0.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.gallagher.mobileconnectsdksample.mobileconnectsdksample"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1700025
        versionName "17.00.025"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = true
    }

    namespace 'com.gallagher.mobileconnectsdksample.mobileconnectsdksample'
}

// set the output file to be sample-app
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "sample-app-${variant.name}.apk"
    }
}

dependencies {
    implementation 'com.gallagher.security:gallaghermobileaccess:+'

    // Salto JustIN Mobile SDK
    implementation(group: 'com.saltosystems', name: 'salto-justinmobile-sdk', version: '3.1.0', ext: 'aar')
    implementation(group: 'com.google.guava', name: 'guava', version: '29.0-android')

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation('com.github.tony19:logback-android:2.0.0') {
        exclude group: 'com.google.android', module: 'android'
    }
    implementation 'org.slf4j:slf4j-api:1.7.30'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
